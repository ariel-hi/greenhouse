#use-added-syntax(jitx)
defpackage sensors :
  import core
  import collections
  import jitx
  import jitx/commands

  import ocdb/defaults
  import ocdb/land-patterns
  import ocdb/symbols
  import ocdb/symbol-utils
  import ocdb/generic-components

  import c-2N3904
  import HDC1080DMBT
  import terminals

defn probe-shape (x:Double, y:Double) -> Polygon :
  val poly =
    Polygon([
      Point(-2.5, 0.0), Point(2.5, 0.0),
      Point(2.5, -20.0), Point(0.0, -25.0), Point(-2.5, -20.0)
    ])
  transform(loc(x, y), poly)

pcb-landpattern probe-lp :
  pad p[1] : oval-smd-pad(0.0, 0.0) at loc(0.0, 0.0)
  ref-label()

pcb-symbol probe-sym :
  pin p at Point(0.0, 0.0)
  layer("foreground") = probe-shape(0.0, 0.0)

pcb-component probe :
  pin p
  landpattern = probe-lp(p => probe-lp.p[1])
  symbol = probe-sym(p => probe-sym.p)
  name = "Probe"
  reference-prefix = "Q"
  mpn = "Probe"

pcb-module soil-circuit :
  pin vcc
  pin gnd
  pin sig

  inst prb : probe[2]
  inst transistor : c-2N3904

  val r1 = res-strap(gnd, sig, 10.0e3)
  val r2 = res-strap(prb[1].p, vcc, 100.0)

  val c1 = cap-strap(gnd, vcc, 1.0e-7)
  val c2 = cap-strap(gnd, vcc, 1.0e-8)

  public net GND (gnd)
  public net VCC (vcc, transistor.p[3])
  public net SIG (sig, transistor.p[2])
  public net PROBE1 (prb[0].p, transistor.p[1])
  public net PROBE2 (prb[1].p)

  place(transistor) at loc(0.0, 0.0) on Top
  place(r1) at loc(0.0, 5.0, 90.0) on Top
  place(r2) at loc(1.27, 5.0, 90.0) on Top
  place(prb[0]) at loc(-5.0, -15.0, 0.0) on Top
  place(prb[1]) at loc(5.0, -15.0, 0.0) on Top

  geom(PROBE1) :
    ; copper-pour(LayerIndex(0), isolate = 0.1) = Circle(0.0, 15.0, 10.0)
    copper(LayerIndex(0)) = probe-shape(-5.0, -5.0)
  
  geom(PROBE2) :
    copper(LayerIndex(0)) = probe-shape(5.0, -5.0)

pcb-module humidity-circuit :
  inst sensor : HDC1080DMBT
  public net GND (sensor.GND)
  public net VCC (sensor.VDD)
  public net SDA (sensor.SDA)
  public net SCL (sensor.SCL)

  place(sensor) at loc(0.0, 0.0) on Top

public pcb-module sensor-circuit :
  inst soil : soil-circuit
  inst humidity : humidity-circuit
  inst terminal3 : c-282834-3
  inst terminal2 : c-282837-2
  net GND (soil.GND, humidity.GND, terminal3.p[1])
  net VCC (soil.VCC, humidity.VCC, terminal3.p[2])
  net SIG (soil.SIG, terminal3.p[3])
  net SDA (humidity.SDA, terminal2.p[1])
  net SCL (humidity.SCL, terminal2.p[2])

  ; Net Symbols
  symbol(GND) = ground-sym
  symbol(VCC) = supply-sym

  place(soil) at loc(0.0, -5.0) on Top
  place(humidity) at loc(0.0, 10.0) on Top
  place(terminal3) at loc(-10.0, 20.0) on Top
  place(terminal2) at loc(10.0, 20.0) on Top

pcb-board sensor-board :
  stackup = default-stackup(4)
  val board-shape = Polygon([
    Point(-7.5, 15.0), Point(7.5, 15.0),
    Point(7.5, -30.0), Point(5.0, -35.0), Point(2.5, -30.0),
    Point(2.5, -10.0), Point(-2.5, -10.0),
    Point(-2.5, -30.0), Point(-5.0, -35.0), Point(-7.5, -30.0)
  ])
  boundary = board-shape
  signal-boundary = board-shape

set-main-module(sensor-circuit)
set-rules(ocdb/rules/sierra-adv-rules)
set-board(sensor-board)

view-board()
view-schematic()